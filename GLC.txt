-><programa_completo> ::= <seccion_declaraciones><seccion_programa>

<seccion_declaraciones> ::= DECLARE<declaraciones>ENDDECLARE 
<declaraciones> ::= <declaraciones><declaracion> | <declaracion>
<declaracion> ::= C_A<lista_id>C_C ASIGN_DCLR C_A<lista_tipo>C_C
<lista_id> ::= <lista_id> COMA ID | ID
<lista_tipo> ::= <lista_tipo> COMA <tipo> | <tipo>
<tipo> ::= TIPO_INT | TIPO_FLOAT | TIPO_STRING

<seccion_programa> ::= BEGIN<sentencias>END
<sentencias> ::= <sentencias><sentencia> | <sentencia>
<sentencia> ::= PRINT CONST_STRING
<sentencia> ::= ID ASIGN <expresion>
<sentencia> ::= ID ASIGN CONST_STRING
<sentencia> ::= <seccion_if> | <seccion_while>
<seccion_while> ::= WHILE P_A<condicion>P_C L_A<sentencias>L_C
<seccion_if> ::= IF P_A<condicion>P_C L_A<sentencias>L_C 
<seccion_if> ::= IF P_A<condicion>P_C L_A<sentencias>L_C ELSE L_A<sentencias>L_C
<condicion> ::= <comparacion>
<condicion> ::= <comparacion><op_logico_bool><comparacion> | OP_NOT <comparacion>
<comparacion> ::= <expresion><op_logico><expresion> | P_A<condicion>P_C
<op_logico_bool> ::= OP_AND | OP_OR 
<op_logico> ::= OP_MENOR | OP_MAYOR | OP_IGUAL | OP_MAYOR_IGUAL | OP_MENOR_IGUAL | OP_DIST
<lista_expresiones> ::= <lista_expresiones>COMA<expresion> | <expresion>
<expresion> ::= <expresion>OP_SUM<termino> | <expresion>OP_REST<termino> | <termino>
<termino> ::= <termino>OP_MULT<factor> | <termino>OP_DIV<factor> | <factor>
<factor> ::= ID | <constante_num> | <func_map> | (<expresion>)
<func_map> ::= MAP P_A <op_map><constante_num> COMA C_A<lista_expresiones>C_C P_C
<op_map> ::= OP_SUM | OP_MULT
<constante_num> ::= CONST_INT | CONST_REAL
